apply plugin: 'groovy'
apply plugin: 'maven'

group = 'com.alexecollins.docker'
version = '1.0.0-SNAPSHOT'

ext {
    localRepoDir = new File(buildDir, 'localRepo')
    generatedTestResourcesDir = new File(buildDir, 'testResources')
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile gradleApi()
    compile('com.alexecollins.docker:docker-java-orchestration-core:2.9.4-SNAPSHOT')

    testCompile localGroovy()
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0', {exclude module: 'groovy-all'}
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
    }
}

task installLocalRepo(type: Upload) {
    description "Installs the artifacts to a local repo for testing"
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository(url: localRepoDir.toURI())
        }
    }
}

task integrationTest(type: Test) {
    dependsOn installLocalRepo
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}

install.dependsOn integrationTest
