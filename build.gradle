apply plugin: 'groovy'
apply plugin: 'maven'

group = 'com.alexecollins.docker'
version = '1.0.0-SNAPSHOT'

ext {
    localRepoDir = new File(buildDir, 'localRepo')
    generatedTestResourcesDir = new File(buildDir, 'testResources')
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile('com.alexecollins.docker:docker-java-orchestration-core:2.5.0')

    testCompile localGroovy()
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0', {exclude module: 'groovy-all'}
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
    }
}

task integrationTest(type: Test) {
    dependsOn install
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integrationTest

install {
    repositories {
        mavenDeployer {
            repository(url: localRepoDir.toURI())
        }
    }
}